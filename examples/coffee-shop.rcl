agent CoffeeShop
  displayName: "Quick Coffee"
  start: TopFlow

  config
    # defines the properties of the RcsBusinessMessagingAgent object from agent.schema.json
    description: "Order your favorite coffee for pickup"
    logoUri: <url https://quickcoffee.com/logo.png>
    color: "#6F4E37"

    phoneNumber: <phone +1-555-0123>
    phoneLabel: "Call Store"

  defaults
    # override the values in the rcl.config.json file
    fallbackMessage: "I didn't understand that. Please choose from the available options."
    messageTrafficType: :promotion
    postbackData: $js> (suggestion) => suggestion?.text?.toLowerCase().replace(/[^a-z0-9]/g, '_')

  flow OrderCoffee
    start: ChooseDrink

    on ChooseDrink
      match @reply.text
        "Espresso" -> set @drink: "espresso" -> Customize
        "Cappuccino" -> set @drink: "cappuccino" -> Customize
        "Latte" -> set @drink: "latte" -> Customize
        "Americano" -> set @drink: "americano" -> Customize
        :default -> InvalidDrink

    on InvalidDrink
      -> ChooseDrink

    on Customize
      match @reply.text
        "Regular" -> set @milk: "regular" -> ChooseSize
        "Skim" -> set @milk: "skim" -> ChooseSize
        "Soy" -> set @milk: "soy", @extraCharge: <money 0.60> -> ChooseSize
        "Oat" -> set @milk: "oat", @extraCharge: <money 0.60> -> ChooseSize
        "No Milk" -> set @milk: "none" -> ChooseSize
        :default -> InvalidMilk

    on InvalidMilk  
      -> Customize

    on ChooseSize
      match @reply.text
        "Small" -> set @size: "small", @price: <money 3.50> -> :end
        "Medium" -> set @size: "medium", @price: <money 4.50> -> :end  
        "Large" -> set @size: "large", @price: <money 5.50> -> :end
        :default -> InvalidSize

    on InvalidSize
      -> ChooseSize

  flow CreateOrder
    start: Menu

    on Menu
      match @reply.text
        "Order Coffee" -> start OrderCoffee
          on :end -> append result to @orderItems -> Menu
          on :cancel -> Menu
          on :error -> Menu
        "Review Order" -> ReviewOrder
        "Cancel Order" -> :cancel

    on ReviewOrder
      match @reply.text
        "Add Another Coffee" -> start OrderCoffee
          on :end -> append result to @orderItems -> ReviewOrder
          on :cancel -> ReviewOrder
          on :error -> ReviewOrder
        "Remove Last Item" -> RemoveItem
        "Confirm Order" -> ConfirmOrder
        "Cancel Order" -> :cancel

    on RemoveItem
      set @orderItems: @orderItems.slice(0, -1) -> ReviewOrder

    on ConfirmOrder
      match @reply.text
        "Yes, Confirm" -> :end
        "No, Go Back" -> ReviewOrder
        "Cancel Order" -> :cancel

  flow TopFlow
    start: Welcome

    on Welcome
      match @reply.text
        "Start Order" -> start CreateOrder
          on :end -> append result to @orders -> ConfirmAllOrders
          on :cancel -> Welcome
          on :error -> OrderError
        "View Menu" -> ShowMenu
        "Store Hours" -> StoreInfo
        :default -> Welcome

    on ShowMenu
      match @reply.text
        "Start Order" -> start CreateOrder
          on :end -> append result to @orders -> ConfirmAllOrders
          on :cancel -> Welcome
          on :error -> OrderError
        "Back" -> Welcome

    on StoreInfo
      match @reply.text
        "Start Order" -> start CreateOrder
          on :end -> append result to @orders -> ConfirmAllOrders
          on :cancel -> Welcome
          on :error -> OrderError
        "Back" -> Welcome

    on OrderError
      match @reply.text
        "Try Again" -> Welcome
        "Contact Support" -> ShowSupport

    on ShowSupport
      match @reply.text
        "Back" -> Welcome

    on ConfirmAllOrders
      match @reply.text
        "Add Another Order" -> start CreateOrder
          on :end -> append result to @orders -> ConfirmAllOrders
          on :cancel -> ConfirmAllOrders
          on :error -> OrderError
        "Confirm All Orders" -> ProcessPayment
        "Cancel All Orders" -> set @orders: [] -> Welcome

    on ProcessPayment
      -> OrderComplete

    on OrderComplete
      -> ThankYou

    on ThankYou
      match @reply.text
        "Start New Order" -> set @orders: [] -> Welcome
        "View Receipt" -> ShowReceipt

    on ShowReceipt  
      match @reply.text
        "Start New Order" -> set @orders: [] -> Welcome
        "Back" -> Welcome

  messages Messages
    richCard Welcome "Welcome to Quick Coffee!" :large
      description: "Get your favorite coffee ready for pickup in minutes!"
      media: <url https://quickcoffee.com/welcome.jpg>
      suggestions
        reply "Start Order"
        reply "View Menu" 
        reply "Store Hours"

    carousel ShowMenu "Our Coffee Menu" :medium
      richCard EspressoCard "Espresso" :compact
        description: "Rich, bold shot of coffee"
        media: <url https://quickcoffee.com/espresso.jpg>
      richCard CappuccinoCard "Cappuccino" :compact
        description: "Espresso with steamed milk foam"
        media: <url https://quickcoffee.com/cappuccino.jpg>
      richCard LatteCard "Latte" :compact
        description: "Espresso with steamed milk"
        media: <url https://quickcoffee.com/latte.jpg>
      richCard AmericanoCard "Americano" :compact
        description: "Espresso with hot water"
        media: <url https://quickcoffee.com/americano.jpg>
      suggestions
        reply "Start Order"
        reply "Back"

    text StoreInfo "We're open Monday-Friday 6am-7pm, weekends 7am-6pm."
      suggestions
        reply "Start Order"
        reply "Back"

    text Menu "What would you like to add to your order?"
      suggestions
        reply "Order Coffee"
        reply "Review Order"
        reply "Cancel Order"

    text OrderError "Sorry, there was an error processing your order. Please try again."
      suggestions
        reply "Try Again"
        reply "Contact Support"

    text ShowSupport "Contact us at support@quickcoffee.com or call +1-555-0199"
      suggestions
        reply "Back"

    richCard ConfirmAllOrders "Confirm Your Orders" :medium
      description: "#{@orders.length} order(s) ready for checkout\\nTotal: $#{@orders.reduce((sum, order) => sum + order.orderItems.reduce((itemSum, item) => itemSum + item.price + (item.extraCharge || 0), 0), 0).toFixed(2)}"
      media: <url https://quickcoffee.com/confirm.jpg>
      suggestions
        reply "Add Another Order"
        reply "Confirm All Orders"
        reply "Cancel All Orders"

    richCard ConfirmOrder "Confirm This Order" :medium
      description: "#{@orderItems.length} item(s) in your order\\nOrder Total: $#{@orderItems.reduce((sum, item) => sum + item.price + (item.extraCharge || 0), 0).toFixed(2)}"
      suggestions
        reply "Yes, Confirm"
        reply "No, Go Back"
        reply "Cancel Order"

    text ChooseDrink "What type of coffee would you like?"
      suggestions
        reply "Espresso"
        reply "Cappuccino" 
        reply "Latte"
        reply "Americano"

    text InvalidDrink "Please choose from our available drinks."
      suggestions
        reply "Espresso"
        reply "Cappuccino"
        reply "Latte" 
        reply "Americano"

    text Customize "Perfect! A #{@drink}. How would you like your milk?"
      suggestions
        reply "Regular"
        reply "Skim"
        reply "Soy +$0.60"
        reply "Oat +$0.60"
        reply "No Milk"

    text InvalidMilk "Please choose a valid milk option."
      suggestions
        reply "Regular"
        reply "Skim"
        reply "Soy +$0.60"
        reply "Oat +$0.60"
        reply "No Milk"

    text ChooseSize "Great choice! What size #{@drink} with #{@milk} milk?"
      suggestions
        reply "Small $3.50"
        reply "Medium $4.50"
        reply "Large $5.50"

    text InvalidSize "Please choose a valid size."
      suggestions
        reply "Small $3.50"
        reply "Medium $4.50"
        reply "Large $5.50"

    richCard ReviewOrder "Review Your Order" :medium
      description: "#{@orderItems.length} item(s) in your order"
      suggestions
        reply "Add Another Coffee"
        reply "Remove Last Item"
        reply "Confirm Order"
        reply "Cancel Order"


    text ProcessPayment "Processing your payment..."

    richCard OrderComplete "Orders Confirmed!" :large
      description: """
        Your orders will be ready for pickup in 5-7 minutes.
        Show this confirmation at the counter.
      """
      media: <url https://quickcoffee.com/confirmed.jpg>

    text ThankYou """
      Thank you for your orders!
      Your pickup time: #{new Date(Date.now() + 6*60000).toLocaleTimeString()}
      Show this confirmation at the counter.
    """
      suggestions
        reply "Start New Order"
        reply "View Receipt"

    richCard ShowReceipt "Your Receipt" :medium
      description: """
        Order Summary:
        #{@orders.map(order => order.orderItems.map(item => `${item.size} ${item.drink} with ${item.milk} milk - $${(item.price + (item.extraCharge || 0)).toFixed(2)}`).join('\n')).join('\n\n')}
        
        Total: $#{@orders.reduce((sum, order) => sum + order.orderItems.reduce((itemSum, item) => itemSum + item.price + (item.extraCharge || 0), 0), 0).toFixed(2)}
      """
      suggestions
        reply "Start New Order"
        reply "Back"

    text OrderCancelled "Your order has been cancelled. Thank you!"